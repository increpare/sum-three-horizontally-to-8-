title sum three to 8!
author increpare
homepage www.increpare.com
color_palette 1
background_color black

run_rules_on_level_start

norepeat_action

========
OBJECTS
========

Background
black

wall
darkgray

spawn 
transparent

player p
white
00...
0....
.....
0....
00...


player_r 
white
...00
....0
.....
....0
...00


deadplayer 
gray
00...
0....
.....
0....
00...


deadplayer_r 
gray
...00
....0
.....
....0
...00

k1 1
red darkred
11111
11111
11011
11111
11111


k2 2
green darkgreen
11111
11101
11111
10111
11111


k3 3
yellow lightbrown
11111
11101
11011
10111
11111



k4 4
brown darkbrown
11111
10101
11111
10101
11111

k5 5
blue darkblue
11111
10101
11011
10101
11111

k6 6
pink purple
11111
10101
10101
10101
11111

spawnfall
transparent

dokill
black
00000
0...0
0...0
0...0
00000

dokill2
black
00000
00000
00.00
00000
00000

tanwas
purple


=======
LEGEND
=======

. = Background
# = Wall
? = spawn
k = k1 or k2 or k3 or k4 or k5 or k6
ko = k1 or k2 or k3 or k4 or k5 or k6

q = k1 and player
w = k2 and player
e = k3 and player
r = k4 and player
t = k5 and player
y = k6 and player

=======
SOUNDS
=======


================
COLLISIONLAYERS
================

Background
wall,k
player, player_r
deadplayer, deadplayer_r
spawn
spawnfall
dokill
dokill2
tanwas

======
RULES
======


right [player deadplayer | player_r deadplayer_r ] -> [stationary player deadplayer | stationary player_r deadplayer_r ]

[player_r]->[]

[dokill][player]->[dokill][player spawnfall]
[dokill2][player]->[dokill2][player spawnfall]

right [action player |]->[action player deadplayer|deadplayer_r]

right [action player  k1|k1]->cancel
right [action player  k2|k2]->cancel
right [action player  k3|k3]->cancel
right [action player  k4|k4]->cancel
right [action player  k5|k5]->cancel
right [action player  k6|k6]->cancel
right [action player  no k|no k]->cancel

[> player | wall]->cancel
right [> player ||wall]->cancel


down [ k | no k ] -> [ > k | ] again
[> k | wall ] -> [ k | wall ]
[> k | stationary ko ] -> [ k | ko ]

[> k ] [ player ] -> [ > k ] [ player spawnfall]

[moving k] [player no spawnfall]-> [ moving k ] [ player spawnfall]

[player] [? no k]->[player spawnfall] [ ? random k] again

right [action player k | ko ]->[tanwas player spawnfall ko | k] again
right [action player k | no ko ]->[tanwas player spawnfall| k] again
right [action player no ko | k ]->[tanwas player spawnfall k | ] again



[dokill2 k]->[] again

[dokill k]->[dokill2 k] again


horizontal [player no spawnfall] [ k1 | k2 | k6 ] -> [player no spawnfall] [ k1 dokill | k2 dokill | k6 dokill ]
horizontal [player no spawnfall] [ k1 | k3 | k5 ] -> [player no spawnfall] [ k1 dokill | k3 dokill | k5 dokill ]
horizontal [player no spawnfall] [ k1 | k4 | k4 ] -> [player no spawnfall] [ k1 dokill | k4 dokill | k4 dokill ]
horizontal [player no spawnfall] [ k1 | k5 | k3 ] -> [player no spawnfall] [ k1 dokill | k5 dokill | k3 dokill ]
horizontal [player no spawnfall] [ k1 | k6 | k2 ] -> [player no spawnfall] [ k1 dokill | k6 dokill | k2 dokill ]
horizontal [player no spawnfall] [ k2 | k1 | k6 ] -> [player no spawnfall] [ k2 dokill | k1 dokill | k6 dokill ]
horizontal [player no spawnfall] [ k2 | k2 | k5 ] -> [player no spawnfall] [ k2 dokill | k2 dokill | k5 dokill ]
horizontal [player no spawnfall] [ k2 | k3 | k4 ] -> [player no spawnfall] [ k2 dokill | k3 dokill | k4 dokill ]
horizontal [player no spawnfall] [ k2 | k4 | k3 ] -> [player no spawnfall] [ k2 dokill | k4 dokill | k3 dokill ]
right [player no spawnfall] [ k2 | k5 | k2 ] -> [player no spawnfall] [ k2 dokill | k5 dokill | k2 dokill ]
horizontal [player no spawnfall] [ k3 | k1 | k5 ] -> [player no spawnfall] [ k3 dokill | k1 dokill | k5 dokill ]
horizontal [player no spawnfall] [ k3 | k2 | k4 ] -> [player no spawnfall] [ k3 dokill | k2 dokill | k4 dokill ]
right [player no spawnfall] [ k3 | k3 | k3 ] -> [player no spawnfall] [ k3 dokill | k3 dokill | k3 dokill ]
right [player no spawnfall] [ k4 | k1 | k4 ] -> [player no spawnfall] [ k4 dokill | k1 dokill | k4 dokill ]


right [dokill][deadplayer|deadplayer_r]->[dokill][|]


[tanwas]->[]

[dokill]->again

late [ spawnfall]->[]

late right [ player | ] -> [ player|player_r]


==============
WINCONDITIONS
==============

no k

=======
LEVELS
=======

(ok)

#####
#q..#
#4.3#
#4.1#
#5.1#
#2.6#
#####

(
Lösung: (0,0) (1,2) (0,3) 
Verhältnis2: 0.153846
Stückzahl: 9
Erfolg: 2
Gesamt: 54
Interessante: 13
Lösungstiefe: 2
)




(easy eough,too short)
######
#e2.4#
#2334#
######

(
Lösung: (2,1) (2,0) 
Verhältnis2: 0.125000
Stückzahl: 7
Erfolg: 1
Gesamt: 19
Interessante: 8
Lösungstiefe: 1
)



(easy enough)
######
#p...#
#...3#
#.1.5#
#5331#
######

(
Lösung: (0,3) (2,3) 
Verhältnis2: 0.083333
Stückzahl: 7
Erfolg: 1
Gesamt: 26
Interessante: 12
Lösungstiefe: 1
)




(easy but ok?)
######
#p...#
#.3.3#
#.1.5#
#35.1#
######

(
Lösung: (1,1) (2,3) 
Verhältnis2: 0.055556
Stückzahl: 7
Erfolg: 1
Gesamt: 49
Interessante: 18
Lösungstiefe: 1
)





(meh, end state one doesn't see coming)

######
#q2.6#
#61.2#
#26.1#
#2661#
######

(
Lösung: (2,3) (2,2) (2,2) (2,3) 
Verhältnis2: 0.010638
Stückzahl: 13
Erfolg: 1
Gesamt: 428
Interessante: 94
Lösungstiefe: 3
)



########
#p.1.6.#
#.23.3.#
#.16.2.#
#.65.24#
#.53.22#
#.44.12#
########

(
Lösung: (2,2) (2,3) (3,3) (0,1) (0,3) (3,5) 
Verhältnis2: 0.002053
Stückzahl: 20
Erfolg: 1
Gesamt: 5453
Interessante: 487
Lösungstiefe: 5
)

#######
#p1.3.#
#.4.2.#
#53.1.#
#46.2.#
#13.1.#
#55.54#
#31.35#
#######

(
Lösung: (1,6) (2,3) (1,4) (2,5) (2,6) (2,6) 
Verhältnis2: 0.001657
Stückzahl: 21
Erfolg: 3
Gesamt: 12181
Interessante: 1811
Lösungstiefe: 5
)



#########
#p...42.#
#..1.15.#
#..6.26.#
#.11462.#
#.43614.#
#.44211.#
#312443.#
#########

(
Lösung: (2,3) (0,3) (3,1) (3,3) (1,6) (5,4) (4,5) 
Verhältnis2: 0.000375
Stückzahl: 29
Erfolg: 10
Gesamt: 302217
Interessante: 26640
Lösungstiefe: 6
)


(hmm OK? not logically solved, but semi-logically)

######
#q.3.#
#3.5.#
#2.43#
#3224#
######

(
Lösung: (2,3) (1,1) (0,2) 
Verhältnis2: 0.017857
Stückzahl: 11
Erfolg: 1
Gesamt: 190
Interessante: 56
Lösungstiefe: 2
)



(hmm hardish?)

######
#p6.6#
#.1.1#
#61.2#
#62.2#
######

(
Lösung: (2,1) (1,1) (2,3) 
Verhältnis2: 0.018519
Stückzahl: 10
Erfolg: 1
Gesamt: 164
Interessante: 54
Lösungstiefe: 2
)



(hard/entertaining?)
######
#t.5.#
#5.22#
#2.22#
#4223#
######

(
Lösung: (0,2) (0,2) (0,3) (0,3) 
Verhältnis2: 0.009709
Stückzahl: 12
Erfolg: 1
Gesamt: 295
Interessante: 103
Lösungstiefe: 3
)


######
#p.22#
#5.21#
#2.16#
#2661#
######

(
Lösung: (0,2) (0,3) (0,3) (0,3) 
Verhältnis2: 0.012821
Stückzahl: 12
Erfolg: 1
Gesamt: 310
Interessante: 78
Lösungstiefe: 3
)



#########
#p......#
#...3...#
#.1.2.6.#
#.4.1.4.#
#.2.5.3.#
#.4.2.42#
#.3.4132#
#########

(
Lösung: (1,3) (4,3) (1,3) (4,5) (3,5) (1,5) 
Verhältnis2: 0.093622
Stückzahl: 19
Erfolg: 160
Gesamt: 14897
Interessante: 1709
Lösungstiefe: 5
)